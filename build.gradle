plugins {
    id 'test-report-aggregation'
    id 'jacoco-report-aggregation'
    alias libs.plugins.sonar
}

reporting {
    reports {
        testAggregateTestReport(AggregateTestReport) {
            testType = TestSuiteType.UNIT_TEST
        }
        testCodeCoverageReport(JacocoCoverageReport) {
            testType = TestSuiteType.UNIT_TEST
        }
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'groovy'
    apply plugin: 'jacoco'

    rootProject.dependencies {
        testReportAggregation project
        jacocoAggregation project
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(JAVA_VERSION)
        }
    }

    test {
        useJUnitPlatform()
        jacoco.enabled = System.getenv('CI')
    }
}