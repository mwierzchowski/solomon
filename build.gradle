plugins {
    id 'test-report-aggregation'
    id 'jacoco-report-aggregation'
    alias libs.plugins.sonar
}

reporting {
    reports {
        testAggregateTestReport(AggregateTestReport) {
            testType = TestSuiteType.UNIT_TEST
        }
        testCodeCoverageReport(JacocoCoverageReport) {
            testType = TestSuiteType.UNIT_TEST
        }
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'groovy'
    apply plugin: 'jacoco'

    rootProject.dependencies {
        testReportAggregation project
        jacocoAggregation project
    }

    dependencies {
        annotationProcessor libs.lombok
        compileOnly libs.lombok
        implementation libs.slf4j.api
        testImplementation libs.spock.core
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    test {
        useJUnitPlatform()
        jacoco.enabled = project.properties['coverage.enabled']
    }

    jacocoTestReport {
        reports.xml.required = true
    }
}